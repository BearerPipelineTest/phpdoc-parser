; ---------------------------------------------------------------------------- ;
;  Type                                                                        ;
; ---------------------------------------------------------------------------- ;

Type
	= Atomic [Union / Intersection]
	/ Nullable

Union
	= 1*(TokenUnion Atomic)

Intersection
	= 1*(TokenIntersection Atomic)

Nullable
	= TokenNullable TokenIdentifier [Generic]

Atomic
	= TokenIdentifier [Generic / Array]
	/ TokenThisVariable
	/ TokenParenthesesOpen Type TokenParenthesesClose [Array]

Generic
	= TokenAngleBracketOpen Type *(TokenComma Type) TokenAngleBracketClose

Array
	= 1*(TokenSquareBracketOpen TokenSquareBracketClose)


; ---------------------------------------------------------------------------- ;
;  Tokens                                                                      ;
; ---------------------------------------------------------------------------- ;

TokenUnion
	= "|" *ByteHorizontalWs

TokenIntersection
	= "&" *ByteHorizontalWs

TokenNullable
	= "?" *ByteHorizontalWs

TokenParenthesesOpen
	= "(" *ByteHorizontalWs

TokenParenthesesClose
	= ")" *ByteHorizontalWs

TokenAngleBracketOpen
	= "<" *ByteHorizontalWs

TokenAngleBracketClose
	= ">" *ByteHorizontalWs

TokenSquareBracketOpen
	= "[" *ByteHorizontalWs

TokenSquareBracketClose
	= "]" *ByteHorizontalWs

TokenComma
	= "," *ByteHorizontalWs

TokenThisVariable
	= %x24.74.68.69.73 *ByteHorizontalWs

TokenIdentifier
	= [ByteBackslash] ByteIdentifierFirst *ByteIdentifierSecond *(ByteBackslash ByteIdentifierFirst *ByteIdentifierSecond) *ByteHorizontalWs


; ---------------------------------------------------------------------------- ;
;  Bytes                                                                       ;
; ---------------------------------------------------------------------------- ;

ByteHorizontalWs
	= %x09 ; horizontal tab
	/ %x20 ; space


ByteIdentifierFirst
	= %x41-5A ; A-Z
	/ %x5F    ; _
	/ %x61-7A ; a-z
	/ %x80-FF

ByteIdentifierSecond
	= %x30-39 ; 0-9
	/ %x41-5A ; A-Z
	/ %x5F    ; _
	/ %x61-7A ; a-z
	/ %x80-FF


ByteBackslash
	= %x5C    ; \
