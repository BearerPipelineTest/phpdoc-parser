/* Written in W3C's EBNF notation (https://www.w3.org/TR/xml/#sec-notation) */

Type
  ::= Atomic (Union | Intersection)?
    | Nullable

Union
  ::= (TUnion Atomic)+

Intersection
  ::= (TIntersection Atomic)+

Nullable
  ::= TNullable Identifier Generic?

Atomic
  ::= TIdentifier (Generic | Array)?
    | TThisVariable
    | TOpenParentheses Type TCloseParentheses Array?

Generic
  ::= TOpenAngleBracket Type (TComma Type)* TCloseAngleBracket

Array
  ::= (TOpenSquareBracket TCloseSquareBracket)+


/* Tokens */
TUnion
  ::= '|' THorizontalWs*

TIntersection
  ::= '&' THorizontalWs*

TNullable
  ::= '?' THorizontalWs*

TOpenParentheses
  ::= '(' THorizontalWs*

TCloseParentheses
  ::= ')' THorizontalWs*

TOpenAngleBracket
  ::= '<' THorizontalWs*

TCloseAngleBracket
  ::= '>' THorizontalWs*

TOpenSquareBracket
  ::= '[' THorizontalWs*

TCloseSquareBracket
  ::= ']' THorizontalWs*

TComma
  ::= ',' THorizontalWs*

TThisVariable
  ::= '$this' THorizontalWs*

TIdentifier
  ::= ('\\'? [a-zA-Z_#x7F-#x10FFFF][0-9a-zA-Z_#x7F-#x10FFFF]*)+ THorizontalWs*

THorizontalWs
  ::= #x09 | #x20
